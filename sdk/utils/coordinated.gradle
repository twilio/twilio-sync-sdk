android {
    buildTypes {
        coordinated {
            initWith(debug)
            matchingFallbacks = ['debug']
            isDefault = true // Doesn't work yet. See: https://issuetracker.google.com/issues/140680608

            applicationIdSuffix ".coordinated"
            versionNameSuffix "-coordinated"
        }
    }
}

configurations {
    coordinated
}

configurations.all {
    resolutionStrategy.preferProjectModules()
}

dependencies {
    coordinatedImplementation project(":${rootProject.name}-android")
}

// Workaround to force android studio select the coordinated built type by default
// See: https://issuetracker.google.com/issues/36988145#comment44
def forceCoordinatedBuild = project.hasProperty("coordinatedBuild.forceBuildVariant") ?
        Boolean.valueOf(project.property("coordinatedBuild.forceBuildVariant")) : false

android {
    variantFilter {
        setIgnore(forceCoordinatedBuild && buildType.name != "coordinated")
    }
}
